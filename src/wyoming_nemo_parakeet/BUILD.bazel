load("@aspect_rules_py//py:defs.bzl", "py_binary", "py_library", "py_pex_binary")
load("@pip//:requirements.bzl", "requirement")
load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("@rules_python//python:pip.bzl", "compile_pip_requirements")

compile_pip_requirements(
    name = "requirements",
    src = "requirements.in",
    extra_args = [
        "--index-url",
        "https://download.pytorch.org/whl/xpu",
        "--extra-index-url",
        "https://pypi.org/simple",
    ],
    requirements_txt = "requirements_lock.txt",
)

XPU_DATA = [
    "@pip//intel_sycl_rt:data",
    "@pip//intel_pti:data",
    "@pip//oneccl:data",
    "@pip//onemkl_sycl_blas:data",
    "@pip//onemkl_sycl_dft:data",
    "@pip//onemkl_sycl_lapack:data",
    "@pip//mkl:data",
    "@pip//intel_cmplr_lib_ur:data",
    "@pip//intel_cmplr_lib_rt:data",
    "@pip//umf:data",
    "@pip//tcmlib:data",
]

py_library(
    name = "handler",
    srcs = ["handler.py"],
    deps = [
        requirement("wyoming"),
    ],
)

py_binary(
    name = "main",
    srcs = ["main.py"],
    data = XPU_DATA,
    env = {
        "BAZEL_DATA_FILES": " ".join(["$(rlocationpaths {})".format(x) for x in XPU_DATA]),
    },
    deps = [
        ":handler",
        "@rules_python//python/runfiles",
        requirement("nemo-toolkit"),
        requirement("torch"),
        requirement("torchaudio"),
        requirement("wyoming"),
    ],
)

## All the packaging targets below are experimental.
py_pex_binary(
    name = "main_pex",
    binary = ":main",
)

filegroup(
    name = "main_zip",
    srcs = [":main"],
    output_group = "python_zip_file",
)

pkg_tar(
    name = "main_tarball",
    srcs = [":main"],
    extension = "tar.gz",
    include_runfiles = True,
)
